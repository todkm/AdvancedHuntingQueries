// # Abuse.ch Recent ThreatFox IOC Feed
let ThreatFoxIOCFeed = materialize (
    (externaldata(report:string)
    [@"https://threatfox.abuse.ch/export/csv/recent/"]
    with (format = "txt"))
    | where report !startswith '#'
    | extend report = parse_csv(report)
    | extend FirstSeenUtc = tostring(report[0])
    | project FirstSeenUtc = todatetime(FirstSeenUtc)
        ,IOCId = trim('[ "]+',tostring(report[1]))
        , IOCValue = trim('[ "]+',tostring(report[2]))
        , IOCType = trim('[ "]+',tostring(report[3]))
        , FKMalware = trim('[ "]+',tostring(report[4]))
        , MalwareAlias = trim('[ "]+',tostring(report[5]))
        , MalwarePrintable = trim('[ "]+',tostring(report[6]))
        , LastSeenUtc = trim('[ "]+',tostring(report[7]))
        , ConfidenceLevel = trim('[ "]+',tostring(report[8]))
        , Reference = trim('[ "]+',tostring(report[9]))
        , Tags = trim('[ "]+',tostring(report[10]))
        , Anonymous = trim('[ "]+',tostring(report[11]))
        , Reporter = trim('[ "]+',tostring(report[12]))
);
let FilteredFeed = ThreatFoxIOCFeed
| where IOCValue != ''
| project IOCValue;
DeviceNetworkEvents
| where Timestamp >= ago(2h)
| where RemoteIP in (FilteredFeed) or RemoteUrl in (FilteredFeed)
